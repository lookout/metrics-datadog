buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1+'
    }
}

subprojects {
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven'
    apply plugin: 'java'

    targetCompatibility = '1.6'
    sourceCompatibility = '1.6'

    repositories {
        jcenter()
    }


    test {
        testLogging {
            /* we want more test failure information, see:
            *  <http://mrhaki.blogspot.com/2013/05/gradle-goodness-show-more-information.html>
            */
            exceptionFormat = 'full'
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }


    /* Make sure we're always publishing the source jars too */
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }


    bintray {
        user = project.bintrayUser
        key = project.bintrayKey
        publish = true
        dryRun = false
        configurations = ['archives']

        pkg {
            userOrg = 'lookout'
            repo = 'systems'
            name = 'metrics-datadog'
            labels = []

            version {
                name = project.version
                vcsTag = "v${project.version}"
                desc = project.description
            }
        }
    }
    bintrayUpload.dependsOn assemble

}
