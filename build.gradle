buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1+'
    }
}

subprojects {
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven'
    apply plugin: 'java'

    targetCompatibility = '1.6'
    sourceCompatibility = '1.6'

    repositories {
        jcenter()
    }


    test {
        testLogging {
            /* we want more test failure information, see:
            *  <http://mrhaki.blogspot.com/2013/05/gradle-goodness-show-more-information.html>
            */
            exceptionFormat = 'full'
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }


    /* Add the sources jar to the list of artifacts to publish */
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }


    bintray {
        user = project.bintrayUser
        key = project.bintrayKey
        publish = true
        dryRun = false
        configurations = ['archives']

        pkg {
            userOrg = 'lookout'
            repo = 'systems'
            name = 'metrics-datadog'
            labels = []

            version {
                name = project.version
                vcsTag = "v${project.version}"
                desc = project.description
            }
        }
    }
    bintrayUpload.dependsOn assemble

}
